Pattern 'en' {
  Directives {
	InstructionDefinitions {
       Instruction 'ribbing-2x2' withLabel '2x2 Rib' {
         Row: Repeat to 2 sts beforeEnd { k2, p2 }, k2
         Row: Repeat to 2 sts beforeEnd { p2, k2 }, p2
       }
       Instruction 'decrease-rows' withLabel 'Decrease Rows' {
         Row: k2, p1 tbl, k2tog, k to 3 sts beforeEnd, p1 tbl, k2
         Row: p2, k1 tbl, p2tog tbl, p to 3 sts beforeEnd, k1 tbl, p2
       }
       Instruction 'stockinette' withLabel 'Two-Row Stockinette Stitch' {
         Row 1 rightSide: k to end
         Row 2: p to end
       }
    }
  }
  GeneralInformation {
    Name: 'Banff'
    Description: 'This pullover has been my standard, fall-winter-casual-wear sweater for three years. Knit it in a durable, classic yarn, and it will become your default sweater for cold weather, too - casual, cozy, easy to wear, and a cinch to knit up in bulky yarn.'
    Dimensions: 'Chest: 50 in, Length 22 in'
    Gauge 'stockinette' {
      StitchGauge: 3.5 stitchesPerInch
      RowGauge: 5.25 rowsPerInch
    }
	Author {
	  FirstName: 'Jenna'
	  LastName:  'Wilson'
	}
  }
  
  Supplies {
    Yarns {
	  YarnType 'wool' [brand:'Tahki',category:'Soho Bulky Tweed'] {
		BallLength: 110 yd
		BallWeight: 100 g
	  }
	  Yarn 'main-color' [typeref:'wool'] {
	    TotalLength: 770 yd
	    TotalWeight: 700 g
	    Color 'discontinued color' [number:304]
	  }
	}
    Needles {
      NeedleType 'the-needle-type' circular { size: 10 US }
      Needle 'the-needle' [typeref:'the-needle-type']
    }
    Accessories
  }
  Directions {
    InstructionGroup 'back' withLabel 'Back' {
       Section { CastOn 86 sts, Repeat 'ribbing-2x2' until measures 8 in }
       Section {
         NextRow rightSide: Repeat 2 times { k21, m1, k22, m1 }, state 90 sts
         NextRow: p to end
         Repeat 'stockinette' 6 additionalTimes
         InformationalMessage withLabel 'If you want the finished length to be longer, work extra rows at this point.'
       }
       Section {
         NextRow: BindOff 4 sts, k to end, state sts
         NextRow: BindOff 4 sts, p to end, state sts
       }
       Section {
         Repeat 'decrease-rows' 24 additionalTimes
         State 34 sts
       }
    }
    InstructionGroup 'right-back-shoulder' withLabel 'Right Shoulder' resetRowCount {
       Section {
         Row rightSide: k2, p1 tbl, k2tog, k7, bindOff allStitches
         Row: p1, p2tog tbl, p to end
         Instruction 'rbs-rows3-4' {
           Row: k2, p1 tbl, k2tog, k to end
           Row: p1, p2tog tbl, p to end 
         }
         Repeat 'rbs-rows3-4' 2 additionalTimes
         State 4 sts remain
       }
       Section {
         NextRow: k2tog, k2tog
         NextRow: bindOff allStitches
       }
       Section {
       	 CastOn 22 sts
       }
    }
    InstructionGroup 'left-back-shoulder' withLabel 'Left Shoulder' resetRowCount {
       Section {
         Row rightSide: bindOff 10 sts, k1, k2tog, k to end
		 Row: P2, k1 tbl, p2tog tbl, p to end
         Instruction 'lbs-rows3-4' {
		   Row: K1, k2tog, k to end
		   Row: P2, k1 tbl, p2tog tbl, p to end
		 }
         Repeat 'lbs-rows3-4' 2 additionalTimes
         State 4 sts remain
       }
       Section {
         NextRow: k2tog, k2tog
         NextRow: bindOff allStitches
       }
    }
    InstructionGroup 'front' withLabel 'Front' resetRowCount {
       Section { CastOn 86 sts, Repeat 'ribbing-2x2' until measures 8 in }
       Section {
         NextRow rightSide: Repeat 2 times { k21, m1, k22, m1 }, state 90 sts
         NextRow: p to end
         Repeat 'stockinette' 6 additionalTimes
         InformationalMessage withLabel 'If you want the finished length to be longer, work extra rows at this point.'
       }
       Section {
         NextRow: BindOff 4 sts, k to end, state sts
         NextRow: BindOff 4 sts, p to end, state sts
       }
       Section {
         Repeat 'decrease-rows' 18 additionalTimes
         State 46 sts
       }
    }
    InstructionGroup 'left-front-shoulder' withLabel 'Left Shoulder' resetRowCount {
       Section {
         Row 1 rightSide: k2, p1 tbl, k2tog, k14, BO all sts
		 Row 2: BO 2 sts, p to end 
         Instruction 'lfs-3-8' {
		   Row [3,5,7]: K1, k2tog, k to end
		   Row [4,6,8]: P2, k1 tbl, p2tog tbl, p to end
		 }
		 Instruction 'lfs-9-10' {
		   Row 9: k to end
		   Row 10: p2, k1 tbl, p2tog tbl, p to end
		 }
	   }
	   Section {
         Repeat 'lfs-9-10' 5 additionalTimes
         State 4 sts remain
       }
       Section {
         NextRow: repeat 2 times { k2tog }
         NextRow: bindOff allStitches
       }
    }
    InstructionGroup 'right-front-shoulder' withLabel 'Right Shoulder' {
       Section {
         CastOn 19 sts
         Row 1 rightSide: BO 2 sts, k to end 
         Row 2: p2, k1 tbl, p2tog tbl, p to end 
         Instruction 'rfs-3-8' {
         Row [3,5,7]: K1, k2tog, k to end
         Row [4,6,8]: P2, k1 tbl, p2tog tbl, p to end
         }
         Instruction 'rfs-9-10' {
           Row 9: k to end
           Row 10: p2, k1 tbl, p2tog tbl, p to end
         }
       }
	   Section {
         Repeat 'rfs-9-10' 5 additionalTimes
         State 4 sts inRow
       }
       Section {
         NextRow: repeat 2 times { k2tog }
         NextRow: bindOff allStitches
       }
    }
    InstructionGroup 'sleeves' withLabel 'Sleeves [make 2]' {
       Section { CastOn 66 sts, Repeat 'ribbing-2x2' 8 additionalTimes }
       Section {
         Row 1 rightSide {
           k2, repeat 3 times { p2tog, k2, p2, k2 }, k to 26 sts beforeEnd,
           repeat 3 times { k2, p2, k2, p2tog tbl }, k2,
           state 60 sts
         }
       }
       Section {
         InformationalMessage withLabel 'Work 7 more rows evenly in this modified ribbing patt across these sts, as follows:'
         Row 2 {
           p2, repeat 3 times { k1, p2, k2, p2 }, p to 23 sts beforeEnd,
           repeat 3 times { p2, k2, p2, k1 }, p2 
         }
         Instruction 'sleeve-first-repeat' {
           Row 3 {
             k2, repeat 3 times { p1, k2, p2, k2 }, k to 23 sts beforeEnd,
             repeat 3 times { k2, p2, k2, p1 }, k2 
           }
           Row 4 {
             p2, repeat 3 times { k1, p2, k2, p2 }, p to 23 sts beforeEnd,
             repeat 3 times { p2, k2, p2, k1 }, p2 
           }
         }
         Repeat 'sleeve-first-repeat' 2 additionalTimes
       }
       Section {
         Row rightSide {
           k2, repeat 3 times { p1, k2, p2tog, k2 }, k to 23 sts beforeEnd,
           repeat 3 times { k2, p2tog tbl, k2, p1 }, k2
           state 54 sts 
         }
       }
       Section {
         InformationalMessage withLabel 'Work 7 more rows evenly in this modified ribbing patt across these sts, as follows:'
         Row {
           p2, repeat 6 times { k1, p2 }, p to 20 sts beforeEnd,
           repeat 6 times { p2, k1 }, p2
         }
         Instruction 'sleeve-second-repeat' {
           Row {
             k2, repeat 6 times { p1, k2 }, k to 20 sts beforeEnd,
             repeat 6 times { k2, p1 }, k2
           }
           Row {
             p2, repeat 6 times { k1, p2 }, p to 20 sts beforeEnd,
             repeat 6 times { p2, k1 }, p2
           }
         }
         Repeat 'sleeve-second-repeat' 2 additionalTimes
       }
       Section {
         Row rightSide {
           k2, repeat 3 times { p1, k2, m1p, p1, k2 }, k to 20 sts beforeEnd, 
           repeat 3 times { k2, p1, m1p, k2, p1 }, k2
           state 60 sts
         }
       }
       Section {
         InformationalMessage withLabel 'Work 7 more rows evenly in this modified ribbing patt across these sts, as follows:'
         Row {
           p2, repeat 3 times { k1, p2, k2, p2 }, p to 23 sts beforeEnd,
           repeat 3 times { p2, k2, p2, k1 }, p2 
         }
         Instruction 'sleeve-third-repeat' {
           Row {
             k2, repeat 3 times { p1, k2, p2, k2 }, k to 23 sts beforeEnd,
             repeat 3 times { k2, p2, k2, p1 }, k2 
           }
           Row {
             p2, repeat 3 times { k1, p2, k2, p2 }, p to 23 sts beforeEnd,
             repeat 3 times { p2, k2, p2, k1 }, p2 
           }
         }
         Repeat 'sleeve-third-repeat' 2 additionalTimes
       }
       Section {
         Row rightSide {
           k2, repeat 3 times { m1p, p1, k2, p2, k2 }, k to 23 sts beforeEnd, 
           repeat 3 times { k2, p2, k2, p1, m1p }, k2, state 66 sts
         }
         Row {
           p2, repeat 6 times { k2, p2 }, k to 26 sts beforeEnd, 
           repeat 6 times { p2, k2 }, p2, state 66 sts
         }
       }
       Section {
         NextRow: Repeat 2 times { k16, m1, k17, m1 }, state 70 sts
         NextRow: p to end 
         Repeat 'stockinette' 21 additionalTimes
       }
       Section {
         NextRow: BO 4, k to end
         NextRow: BO 4, p to end, state 62 sts
       }
       Section { Repeat 'decrease-rows' 27 additionalTimes, state 8 sts }
       Section { NextRow rightSide: k2, p1 tbl, k2tog, p1 tbl, k2 }
       Section { NextRow wrongSide: p2, k1 tbl, k2tog tbl, p2 }
       Section { NextRow: BO all sts }
    }
    InstructionGroup 'finishing' withLabel 'Finishing' resetRowCount {
       Section { InformationalMessage withLabel 'Block pieces. Sew raglan edges of Sleeves to armhole edges of Front and Back. Sew underarm, sleeve, and side seams.' }
       Section { InformationalMessage withLabel 'Starting at seam along the neckline, pick up 72 sts for Collar as follows: pick up 4 sts along Sleeve; 15 sts along side Front neck; 8 sts along center Front neck; 15 sts along side Front neck; 4 sts along Sleeve; 8 sts along side Back neck; 10 sts along center Back neck; and 8 sts along side Back neck.' }
       Section { InformationalMessage withLabel 'Mark beginning of round. Working from RS of garment, [k2, p2] around all 72 sts for 8 rounds. ' }
       Section { InformationalMessage withLabel 'On next round, [k2, p2tog, k2, p2] 9 times. Work 5 more rounds even in this modified ribbing patt.' }
       Section { InformationalMessage withLabel 'On next round, [k2, p1, k2, p2tog] 9 times. Work 9 more rounds evenly in this modified ribbing patt.' }
       Section { InformationalMessage withLabel 'BO all sts loosely in patt. Weave in any loose ends, then go snuggle.' }
    }
  }
}